---
date_added: 2019-09-20
title: Moeshouse 2 way 2 gang MS-104B
category: switch
type: Relay
standard: eu
link: https://www.aliexpress.com/item/33047521274.html?spm=a2g0s.9042311.0.0.5f705c0fkcQLzV
image: https://user-images.githubusercontent.com/39533759/65378668-22f5b080-dcbc-11e9-9401-8d906b204914.jpg
template: {"NAME":"moeshouse2gang","GPIO":[0,0,17,0,160,0,0,0,43,42,21,22,0],"FLAG":0,"BASE":18} 
link2: 
---

The device use frequency detection circuits connected to GPIO12 and GPIO13 of the LM1 ESP8266 chip. At the moment you have to use a script to make it work with Tasmota. Script is not supported with precompiled binary (at least 6.6.0) so you have to compile your own fw. Personally recommended gitpod: 

[https://gitpod.io/#https://github.com/benzino77/tasmocompiler](https://gitpod.io/#https://github.com/benzino77/tasmocompiler)

Please note that the script not working good with core 2.3.0! 
Below two scripts which do something like switchmode4 and switchmode1. The script for switchmode4 is created by @thxthx0 and modified by me for switchmode1


SwitchMode1
```console
>D
sw1=0
sw2=0
cnt1=0
cnt2=0
timer1=0
timer2=0
pwr1=0
pwr2=0


>B
=>print "WiFi 2-Gang Switch Script"



>F
; Counter1/2 and Relay1/2 configured in template
cnt1=pc[1]
cnt2=pc[2]

if chg[cnt1]>0
then
; counter1 has changed, switch is on, reset counter
sw1=1
->Counter1 0
else
; no change switch is off
sw1=0
endif

if chg[cnt2]>0
then
; counter2 has changed, switch is on, reset counter
sw2=1
->Counter2 0
else
; no change switch is off
sw2=0
endif

; 100 ms timer
timer1+=1
timer2+=1

if sw1==1
and timer1>2
and pwr1==0

then
=>Power1 1
pwr1=1
endif

if sw1==0
and timer1<=2
and pwr1==1

then
=>Power1 0
pwr1=0
endif

if sw1==0
then
timer1=0
endif

if sw2==1
and timer2>2
and pwr2==0
then
=>Power2 1
pwr2=1
endif

if sw2==0
and timer2<=2
and pwr2==1
then
=>Power2 0
pwr2=0
endif

if sw2==0
then
timer2=0
endif

```

SwitchMode4

```lua
>D
sw1=0
sw2=0
cnt1=0
cnt2=0
timer1=0
timer2=0
toggle1=0
toggle2=0

>B
=>print ""WiFi 2-Gang Switch Script""

>F
; Counter1/2 and Relay1/2 configured in template
cnt1=pc[1]
cnt2=pc[2]

if chg[cnt1]>0
then
; counter1 has changed, switch is on
sw1=1
else
; no change switch is off
sw1=0
endif

if chg[cnt2]>0
then
; counter2 has changed, switch is on
sw2=1
else
; no change switch is off
sw2=0
endif

; 100 ms timer
timer1+=1
timer2+=1

if sw1==0
and timer1>2
and timer1<30
then
;=>print short press1
toggle1^=1
=>Power1 %toggle1%
endif

if sw1==0
then
timer1=0
endif

if sw2==0
and timer2>2
and timer2<30
then
;=>print short press2
toggle2^=1
=>Power2 %toggle2%
endif

if sw2==0
then
timer2=0
endif
```

![flash_pins](https://user-images.githubusercontent.com/39533759/65378708-98618100-dcbc-11e9-8b38-f0156d719a38.JPG)
![rewers_site](https://user-images.githubusercontent.com/39533759/65378740-453bfe00-dcbd-11e9-8695-e08d47b92855.JPG)



